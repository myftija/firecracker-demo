# Parameters
# log_file: path to the input log file
# output_file: path to the output PNG file
# series_name: name for the data series in the plot
# plot_title: main title for the plot

set term png size 1920,1080 enhanced
set output output_file

set title plot_title font ",24"

set key left bottom

# Add axis labels
set xlabel "vm #"
set ylabel "ms"

stats log_file u 1:3 nooutput

set xrange [0:STATS_max_x]
set yrange [0:STATS_max_y+50]

set label 1 gprintf("Maximum = %g ms", STATS_max_y) at STATS_pos_max_y-30, STATS_max_y+18
set arrow from STATS_pos_max_y, STATS_max_y+15 to STATS_pos_max_y, STATS_max_y lc rgb 'black'

set label 2 gprintf("Minimum = %g ms", STATS_min_y) at STATS_pos_min_y-25, STATS_min_y-80
set arrow from STATS_pos_min_y, STATS_min_y-77 to STATS_pos_min_y, STATS_min_y lc rgb 'black'

f(x) = mean_y
fit f(x) log_file u 1:3 via mean_y

stddev_y = sqrt(FIT_WSSR / (FIT_NDF + 1))

set label 3 gprintf("'Total' mean avg = %g ms", mean_y) at STATS_max_x/2-50, 100
set label 4 gprintf("Standard deviation = %g", stddev_y) at STATS_max_x/2-50, 80

plot mean_y-stddev_y with filledcurves y1=mean_y lt 1 lc rgb "#b3ffb3" notitle, \
     mean_y+stddev_y with filledcurves y1=mean_y lt 1 lc rgb "#b3ffb3" title "Standard deviation", \
     mean_y w l lt 1 lc rgb "#ffff66" lw 3 title "Mean average", \
     log_file u 1:3 title series_name w p lc rgb 'orange' pt 13 ps 1, \
     STATS_min_y w l lc rgb 'blue' notitle, \
     STATS_max_y w l lc rgb 'blue' notitle

